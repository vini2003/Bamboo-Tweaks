plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.mod_name
group = project.mod_group
version = project.mod_version + "+" + project.minecraft_version

loom {
    accessWidener = file("src/main/resources/eco.accesswidener")
}

repositories {
    maven {
        name = "Hephaestus"
        url = "https://hephaestus.dev/release"
    }

    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }

    maven {
        name = "shedaniel"
        url = "https://maven.shedaniel.me/"
    }
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // Yarn
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Fabric Loader
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Myron
    modImplementation "dev.monarkhes:myron:${project.myron_version}"

    // Mod Menu
    modImplementation "io.github.prospector:modmenu:${project.mod_menu_version}"

    // Cloth Config
    modApi("me.shedaniel.cloth:cloth-config:${project.cloth_config_version}:fabric") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Cardinal Components API
    modImplementation "com.github.OnyxStudios.Cardinal-Components-API:Cardinal-Components-API:${project.cardinal_components_api_version}"

    // Cloth API
    modApi "me.shedaniel.cloth.api:cloth-api:${project.cloth_api_version}"
    include "me.shedaniel.cloth.api:cloth-api:${project.cloth_api_version}"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
